dnl $Id: configure.in,v 1.40 2002/11/04 00:44:15 markus Rel $
dnl Process this file with autoconf to produce a configure script.

AC_REVISION($Revision: 1.40 $)

AC_INIT()

MAJOR_VERSION=0
MINOR_VERSION=6
MICRO_VERSION=01
RELEASE=$MAJOR_VERSION.$MINOR_VERSION
VERSION=$RELEASE.$MICRO_VERSION
RELEASE=$MAJOR_VERSION.$MINOR_VERSION
AC_DEFINE_UNQUOTED(LIB_RELEASE, "$RELEASE")
AC_DEFINE_UNQUOTED(SYSTEM, UNIX)
AC_SUBST(VERSION)
AC_SUBST(RELEASE)

LIBVERSION=0:1:0
LIBRELEASE=0:1:0
AC_SUBST(LIBVERSION)
AC_SUBST(LIBRELEASE)

SUBDIRS="Common"

GTKMM_NEEDED_MAYOR=1
GTKMM_NEEDED_MINOR=2
GTKMM_NEEDED_MICRO=7
GTKMM_MINVERSION=$GTKMM_NEEDED_MAYOR.$GTKMM_NEEDED_MINOR.$GTKMM_NEEDED_MICRO

dnl AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE(General, $VERSION)
AM_CONFIG_HEADER(gzo-cfg.h)

AC_LANG_C
AC_LANG_CPLUSPLUS

AC_C_BIGENDIAN

ALL_LINGUAS="de en es"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AM_GNU_GETTEXT

dnl Checks for libraries.
AC_CHECK_LIB(stdc++, main)
AC_CHECK_LIB(gmp, main)
AC_CHECK_LIB(pthread, main)

dnl Checks for header files.
dnl AC_CHECK_HEADERS(assert.h errno.h fstream.h iomanip.h iostream.h netinet/in.h)
dnl AC_CHECK_HEADERS(netdb.h stddef.h stdexcept stdio.h streambuf.h streamb.h)
dnl AC_CHECK_HEADERS(string sys/socket.h sys/stat.h sys/types.h syslog.h time.h)

AC_HEADER_DIRENT

AC_MSG_CHECKING(for header regex.h)
AC_CACHE_VAL(ac_cv_header_regex_h, ac_cv_header_regex_h=$ac_cv_header_regex_h)
if test -z $ac_cv_header_regex_h; then
AC_TRY_COMPILE(
   [ #include <sys/types.h>
     #include <regex.h>],
   [],
   [ ac_cv_header_regex_h=yes
     AC_DEFINE(HAVE_REGEX_H)],
   [ ac_cv_header_regex_h=no])
fi
AC_MSG_RESULT([$ac_cv_header_regex_h])
if test "$ac_cv_header_regex_h" != "yes"; then
   AC_CHECK_HEADERS(regexp.h)
fi

if test "$enable_corba" != "no"; then
   AC_MSG_CHECKING(for header CORBA.h)
   AC_CACHE_VAL(ac_cv_header_CORBA_h, ac_cv_header_CORBA_h=$ac_cv_header_CORBA_h)
   if test -z $ac_cv_header_CORBA_h; then
   AC_TRY_COMPILE([#include <CORBA.h>], [],
      [ ac_cv_header_CORBA_h=yes],
      [ ac_cv_header_CORBA_h=no])
   fi
   AC_MSG_RESULT([$ac_cv_header_CORBA_h])
   if test "$ac_cv_header_CORBA_h" == "yes"; then
      SUBDIRS="$SUBDIRS CORBA"
   else
      AC_MSG_WARN([CORBA is not installed; subdir CORBA will not be made])
   fi
fi

dnl Check gtkmm version
AM_PATH_GTKMM($GTKMM_MINVERSION, [SUBDIRS="$SUBDIRS X-windows"],
              [AC_MSG_WARN([Subdir X-windows will not be made])])
AC_SUBST(SUBDIRS)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STAT
AC_STRUCT_TM
AC_CHECK_TYPES([ssize_t, socklen_t])

dnl Checks for library functions.
AC_FUNC_FNMATCH
AC_FUNC_CLOSEDIR_VOID

AC_CHECK_FUNCS(timegm strfmon)

AC_MSG_CHECKING(if signal.h defines a SIGBUS)
AC_CACHE_VAL(ac_cv_HAVE_SIGBUS, ac_cv_HAVE_SIGBUS=$ac_HAVE_SIGBUS)
AC_TRY_COMPILE([#include <signal.h>], [signal (SIGBUS, SIG_DFL);],
               [AC_DEFINE(HAVE_SIGBUS) HAVE_SIGBUS=yes])
AC_MSG_RESULT($HAVE_SIGBUS)

dnl Enable special features
AC_ARG_ENABLE(debug,
              AC_HELP_STRING([[[[[--enable-debug[=LEVEL]]]]]],
	                     [Enables additional traces]),
              if test "$enableval" != "no"; then
	      	  test "$enableval" == "yes" && enableval=0
                  CXXFLAGS="-g -O2 -DTRACELEVEL=$enableval";
              else
                  CXXFLAGS="-O2 -UTRACELEVEL"; fi)
AC_ARG_ENABLE(checks,
              AC_HELP_STRING([[[[[--enable-checks[=LEVEL]]]]]],
	                     [Enables additional (internal) checks]),
              if test "$enableval" != "no"; then
	      	  test "$enableval" == "yes" && enableval=0
                  CXXFLAGS="$CXXFLAGS -DCHECK=$enableval";
              else
                  CXXFLAGS="$CXXFLAGS -DNDEBUG -UCHECK"; fi)
AC_ARG_ENABLE(corba,
              AC_HELP_STRING([--enable-corba],
                             [Enables the check for an CORBA-implementation]))

AC_OUTPUT([Makefile Common/Makefile Common/Tests/Makefile X-windows/Makefile \
           X-windows/Icons/Makefile CORBA/Makefile intl/Makefile po/Makefile.in \
           doc/Makefile CORBA/Tests/Makefile])
